# Copyright 2014 Square Inc.
#
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
#
#        http://www.apache.org/licenses/LICENSE-2.0
#
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.

$(window).ready () ->

  # GENERAL, APPLIES TO BOTH COMMITS AND ARTICLES

  $('tbody tr').click () ->
    toggle($(this))

  toggle = (row) ->
    entry = row.find('.description')
    full_description = entry.data('full-description')
    short_description = entry.data('short-description')
    sub_description = entry.data('sub-description')

    if not entry.data("orig-height")
      orig_height = entry.prop('scrollHeight')
      entry.data("orig-height", orig_height)

    if full_description
      if row.hasClass("expanded")
        orig_height = entry.data("orig-height")
        entry.animate({height: orig_height}, 250, () ->
          entry.text(short_description)
          row.removeClass("expanded")
        )
        row.removeClass("expanded")
      else
        $(".expanded").each () ->
          toggle($(this))

        orig_height = entry.data("orig-height")
        orig_width = entry.width()

        entry.height(orig_height)
        entry.width(orig_width)

        entry.text(full_description)
        if sub_description
            entry.append('</br>')
            entry.append($('<strong/>').text(sub_description))

        entry.animate({height: entry.prop('scrollHeight')}, 250, () ->
          row.addClass("expanded")
        )
        row.addClass("expanded")

  $("table").find("input, select, button, a").click (e) ->
    e.stopPropagation()

  $(".datepicker").datepicker(
    autoclose: true
    startDate: new Date()
  )

  $('.edit_commit select, .edit_commit input[type="text"], .edit_article select, .edit_article input[type="text"]').change () ->
    select = $(this)
    form = select.closest('form')
    authParam = $('meta[name=csrf-param]').attr('content')
    authToken = $('meta[name=csrf-token]').attr('content')
    data = form.serializeObject()
    data[authParam] = authToken

    $.ajax(form.attr('action'),
      data: data,
      type: 'PATCH',
      success: () ->
        select.css('backgroundColor', '#dfd')
              .delay(200)
              .animate({backgroundColor: "white"}, 300)
      ,
      error: () ->
        new Flash('alert').text("Update failed!")
    )

  # COMMIT SPECIFIC

  $('#new_commit_project_id').change () ->
    project = $(this).val()
    $('#new_commit').attr('action', '/projects/' + project + '/commits')
    $.cookie 'home_index_new_commit_project', project, expires: 7

  commits_filter_form = $('#commits-filter-form')

  commits_filter_form.find('input#commits_filter__hide_exported').change ->
    $.cookie 'home_index__commits_filter__hide_exported', $(this).is(':checked')

  commits_filter_form.find('input#commits_filter__hide_autoimported').change ->
    $.cookie 'home_index__commits_filter__hide_autoimported', $(this).is(':checked')

  commits_filter_form.find('input#commits_filter__show_only_mine').change ->
    $.cookie 'home_index__commits_filter__show_only_mine', $(this).is(':checked')


  # ARTICLE SPECIFIC

  $('#request_new_project_article_button').click ->
    window.location = $('#new_project_article_id').val()
