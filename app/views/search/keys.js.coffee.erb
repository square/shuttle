# Copyright 2014 Square Inc.
#
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
#
#        http://www.apache.org/licenses/LICENSE-2.0
#
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.

$(window).ready ->
  # Methods
  localeOrder = []

  formatTable = (keyTable, locales) ->
    keyTable.empty()
    thead = $('<thead/>').appendTo(keyTable)
    header = $('<tr/>').appendTo(thead)
    $('<th/>').text("String ID").appendTo header
    $('<th/>').text("Key").appendTo header

    if locales
      for locale in locales
        do (locale) ->
          $('<th/>').text(locale.rfc5646).appendTo header

  addKey = (keyTable, key) ->
    tr = $('<tr/>').appendTo(keyTable)
    idTD = $('<td/>').text(key.id).appendTo(tr)
    keyTD = $('<td/>').text(key.original_key).appendTo(tr)
    $('<br/>').appendTo(keyTD)
    $('<small/>').addClass('muted').text(key.source).appendTo(keyTD)
    for locale in localeOrder
      do (locale) ->
        translation = (trans for trans in key.translations when trans.locale.rfc5646 == locale)[0]
        if translation?
          klass = if translation.translated && translation.approved
            'text-success'
          else if translation.translated
            'text-info'
          else if translation.approved == false
            'text-error'
          else
            'muted'

          copy = if translation.translated == false
            "(not yet translated)"
          else if /\A\s*\z/.test(translation.copy)
            "(blank string)"
          else
            translation.copy[0..30]

          td = $('<td/>').appendTo(tr)
          $('<a/>').attr('href', translation.url).addClass(klass).text(copy).appendTo(td)
        else
          td = $('<td/>').appendTo(tr)

  # Window on-ready
  keyTable = $('#keys')
  keySearchForm = $('#key-search-form')
  prefillForm = ->
    keySearchForm.find('[name]')
    .not('meta')
    .not('[type=hidden]')
    .not('[type=submit]').each (i, elt) ->
      prefillVal = $.url().param( $(elt).attr('name') )
      if $(elt).is('select') and !prefillVal
        $(elt).prop('selectedIndex', 0)
      else
        $(elt).val(prefillVal)
  prefillForm()

  makeKeyURL = -> "#{keyTable.data('url')}?#{keySearchForm.serialize()}"

  keyScroll = keyTable.infiniteScroll makeKeyURL,
    windowScroll: true
    renderer: (keys) =>
      for key in keys
        do (key) -> addKey(keyTable, key)

  submitSearch = ->
    $.ajax keyTable.data('url'),
      data: keySearchForm.serialize() + '&metadata=true'
      success: (metadata) ->
        keyScroll.reset()
        # record the order of locales for consistency
        localeOrder = (locale.rfc5646 for locale in metadata.locales)
        formatTable(keyTable, metadata.locales)
        keyScroll.loadNextPage()

      error: ->
        new Flash('alert').text("Couldn't load search results")

  keySearchForm.submit ->
    window.history.pushState("params", "", "?#{keySearchForm.serialize()}")
    submitSearch()
    false

  window.onpopstate = ->
    prefillForm()
    submitSearch()
