# Copyright 2014 Square Inc.
#
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
#
#        http://www.apache.org/licenses/LICENSE-2.0
#
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.

$(window).ready ->
  firstTime = true

  formatTable = (commitTable) ->
    commitTable.empty()
    thead = $('<thead/>').appendTo(commitTable)
    header = $('<tr/>').appendTo(thead)
    $('<th/>').text('SHA').appendTo(header)
    $('<th/>').text('Project').appendTo(header)
    $('<th/>').text('Status').appendTo(header)


  addCommit = (commitTable, commit) ->
    tr = $('<tr/>').appendTo(commitTable)

    $('<a>', {
      text: commit.revision
      href: commit.url
    }).appendTo( $('<td/>').appendTo tr )
    $('<td/>').text(commit.project.name).appendTo(tr)

    klass = ''
    status = ''
    if commit.ready
      status = 'Ready'
      klass = 'text-success'
    else if commit.loading
      status = 'Loading'
      klass = 'text-info'
    else # In progress
      status = 'In Progress'
      klass = ''
    $('<td/>').text(status).addClass(klass).appendTo(tr)

  commitSearchForm = $('#commit-search-form')
  commitTable = $('#commits')
  prefillForm = ->
    commitSearchForm.find('[name]')
      .not('meta')
      .not('[type=hidden]')
      .not('[type=submit]').each (i, elt) ->
        prefillVal = $.url().param( $(elt).attr('name') )
        if $(elt).is('select') and !prefillVal
          $(elt).prop('selectedIndex', 0)
        else
          $(elt).val(prefillVal)
  prefillForm()

  formatTable(commitTable)
  makeCommitURL = -> "#{commitTable.data('url')}?#{commitSearchForm.serialize()}"
  
  commitScroll = commitTable.infiniteScroll makeCommitURL,
    windowScroll: true
    renderer: (commits) ->
      for commit in commits
        do (commit) -> addCommit(commitTable, commit)
    dataSourceOptions: {type: 'GET'}

  submitSearch = ->
    $.ajax commitTable.data('url'),
      data: commitSearchForm.serialize()
      success: (commits) ->
        commitScroll.reset()
        formatTable(commitTable)
        commitScroll.loadNextPage()
      error: =>
        new Flash('alert').text("Couldn't load search results");

  commitSearchForm.submit ->
    window.history.pushState("params", "", "?#{commitSearchForm.serialize()}")
    submitSearch()
    false

  window.onpopstate = ->
    unless firstTime
      prefillForm()
      submitSearch()
    firstTime = false


